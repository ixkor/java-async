buildscript {
    repositories {
        jcenter()
        mavenLocal()
    }
    dependencies {
        classpath 'com.jfrog.bintray.gradle:gradle-bintray-plugin:0.6'
    }
}

subprojects {
    if (JavaVersion.current().isJava8Compatible()) {
        tasks.withType(Javadoc) {
            options.addStringOption('Xdoclint:none', '-quiet')
        }
    }

    apply plugin: 'java'
    apply plugin: 'maven'
    apply plugin: 'maven-publish'
    apply plugin: 'com.jfrog.bintray'

    sourceCompatibility = 1.7
    targetCompatibility = 1.7

    task javadocJar(type: Jar) {
        classifier = 'javadoc'
        from javadoc
    }

    task sourcesJar(type: Jar) {
        classifier = 'sources'
        from sourceSets.main.allSource
    }

    artifacts {
        archives sourcesJar, javadocJar
    }

    publishing {
        publications {
            mavenJava(MavenPublication) {
                from components.java

                artifact sourcesJar
                artifact javadocJar
            }
        }
    }

    repositories {
        jcenter()
        mavenLocal()
    }

    bintray {
        if (project.hasProperty("bintrayApiKey")) {
            user = bintrayUserName
            key = bintrayApiKey
        }

        publications = ['mavenJava']
        publish = true
        pkg {
            repo = 'maven'
            name = project.name
            websiteUrl = 'https://github.com/ixkor/java-async'
            issueTrackerUrl = ''
            vcsUrl = 'https://github.com/ixkor/java-async.git'
            licenses = ['Apache-2.0']
            publicDownloadNumbers = true
        }
    }
}